// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    Inc16(in= oldOut, out= outPlusAux);
    Mux16(a= oldOut, b= outPlusAux, sel= inc, out= outPlus);
    Mux16(a= outPlus, b= in, sel= load, out= outPlusLoad);
    Mux16(a= outPlusLoad, b[0..15]= false, sel= reset, out= outPlusLoadReset);
    Register(in= outPlusLoadReset, load= true, out= out, out=oldOut);
}